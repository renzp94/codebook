---
title: 第三章：简单的数据库实现(源码)
slug: Lisp
time: 2021-3-25 21:52:53
author: renzp94
status: done
---

第二章是一个简单的数据库例子，通过编写一个 cd 信息的操作，来讲述知识点。

主要设置的知识点：

- 全局变量
- 格式化输出
- 列表的使用
- 文件的读写
- 宏

源代码：

```commonlisp
;;定义一个全局变量，用于存放数据
(defvar *db* nil)

;;制作一个CD需要的信息
(defun make-cd (title artist rating ripped)
  (list :title title :artist artist :rating rating :ripped ripped))

;;将一条CD信息记录到全局变量中
(defun add-record (cd) (push cd *db*))

;;格式化输出数据
(defun dump-db ()
  (format t "~{~{~A: ~10t~A~%~}~%~}" *db*))

;;交互信息
(defun prompt-read (prompt)
  (format *query-io* "~A: " prompt)
  (force-output *query-io*)
  (read-line *query-io*))

;;交互获取CD信息
(defun prompt-for-cd ()
  (make-cd
   (prompt-read "Title")
   (prompt-read "Artist")
   (or (parse-integer (prompt-read "Rating") :junk-allowed t) 0)
   (y-or-n-p "Ripped [y/n]: ")))

;;添加任意个CD信息
(defun add-cds ()
  (loop (add-record (prompt-for-cd))
     (if (not (y-or-n-p "Another? [y/n]: ")) (return))))

;;将所有CD信息保存到文件中
(defun save-db (filename)
  (with-open-file (out filename
		       :direction :output
		       :if-exists :supersede)
    (with-standard-io-syntax
      (print *db* out))))

;;加载文件中的CD信息
(defun load-db (filename)
  (with-open-file (in filename)
    (with-standard-io-syntax
      (setf *db* (read in)))))
;;通用查询
(defun select (selector-fn)
  (remove-if-not selector-fn *db*))

;;根据artist查询
(defun artist-selector (artist)
  #'(lambda (cd) (equal (getf cd :artist) artist)))
;;比较根据键比较值
(defun make-comparison-expr (field value)
  `(equal (getf cd ,field) ,value))
;;循环比较
(defun make-comparisons-list (fields)
  (loop while fields
     collecting (make-comparison-expr (pop fields) (pop fields))))
;;定义一个where宏
(defmacro where (&rest clauses)
  `#'(lambda (cd) (and ,@(make-comparisons-list clauses))))
;;删除函数
(defun delete-rows (selector-fn)
  (setf *db* (remove-if selector-fn *db*)))
```

在 eamcs 中使用 C-x C-f 新建一个文件，输入文件名之后回车。

将上述源代码粘贴复制到此文件中，使用 C-x C-s 保存文件。

保存完成之后，在 emacs 中使用 M-x 输入 slime 回车，启动 REPL(lisp 环境)。

在 REPL 中输入：(load "CD.lisp")回车，加载保存的文件。

到这一步，则可使用上述源码中的所有函数，接下来进行测试。

测试代码如下：

```commonlisp
CL-USER> *db*
NIL
CL-USER> (add-cds)
Title: BangBom
Artist: Are
Rating: 5


Ripped [y/n]:  (y or n) y


Another? [y/n]:  (y or n) y
Title: So happy
Artist: Bre
Rating: 8


Ripped [y/n]:  (y or n) n


Another? [y/n]:  (y or n) n


NIL
CL-USER> (dump-db)
TITLE:    So happy
ARTIST:   Bre
RATING:   8
RIPPED:   NIL


TITLE:    BangBom
ARTIST:   Are
RATING:   5
RIPPED:   T


NIL
CL-USER> (save-db "CD.db")
((:TITLE "So happy" :ARTIST "Bre" :RATING 8 :RIPPED NIL)
 (:TITLE "BangBom" :ARTIST "Are" :RATING 5 :RIPPED T))
CL-USER> (select (where :title "BangBom"))
((:TITLE "BangBom" :ARTIST "Are" :RATING 5 :RIPPED T))
CL-USER> (select (where :artist "Bre"))
((:TITLE "So happy" :ARTIST "Bre" :RATING 8 :RIPPED NIL))
CL-USER> 
```
